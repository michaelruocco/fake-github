apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'idea'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'jacoco'
apply plugin: 'com.github.kt3k.coveralls'

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.0.1'
    }
}

project.ext {
    artifactId = "fake-github"
    group = 'com.github.michaelruocco'
    version = '1.0.0'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile 'com.github.tomakehurst:wiremock-standalone:2.1.11'
    compile 'org.slf4j:slf4j-simple:1.7.21'
    compile 'junit:junit:4.12'
    compile 'org.apache.commons:commons-lang3:3.4'
    compile 'org.apache.httpcomponents:httpclient:4.5.2'

    testCompile 'org.assertj:assertj-core:3.5.2'
    testCompile 'com.fasterxml.jackson.core:jackson-core:2.8.1'
    testCompile sourceSets.main.output
}

run {
    mainClassName = mainClassName
}

def getMainClassName() {
    'uk.co.mruoc.fake.github.FakeGithubRunner'
}

task standaloneJar(type: Jar) {
    manifest {
        attributes 'Implementation-Version': project.ext.version
        attributes 'Main-Class': mainClassName
    }
    baseName = project.ext.artifactId + '-standalone'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

def getStandaloneArtifactId() {
    project.ext.artifactId + '-standalone'
}

if (project.hasProperty("standalone")) {
    jar {
        manifest {
            attributes 'Implementation-Version': project.ext.version
            attributes 'Main-Class': mainClassName
        }
        baseName = standaloneArtifactId
        from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    }
}

gradle.taskGraph.whenReady { taskGraph ->
    if (taskGraph.hasTask(uploadArchives)) {
        signing {
            required true
            sign configurations.archives
        }
    }
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                packaging 'jar'
                name 'Fake Github'
                description 'Code library to mock github api'
                groupId project.ext.group
                if (project.hasProperty('standalone')) {
                    artifactId standaloneArtifactId
                } else {
                    artifactId project.ext.artifactId
                }
                version project.ext.version
                url 'https://github.com/michaelruocco/fake-github'

                developers {
                    developer {
                        id 'mruoc'
                        name 'Michael Ruocco'
                        email 'michael.ruocco@hotmail.com'
                    }
                }

                scm {
                    connection 'scm:git:git@github.com/michaelruocco/fake-github.git'
                    developerConnection 'scm:git:git@github.com/michaelruocco/fake-github.git'
                    url 'git@github.com/michaelruocco/fake-github.git'
                }

                licenses {
                    license {
                        name 'BSD License'
                        url 'http://www.opensource.org/licenses/bsd-license.php'
                        distribution 'repo'
                    }
                }
            }
        }
    }
}